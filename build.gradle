plugins {
    id "com.jfrog.bintray" version "1.5"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

def baseVersion = "2.0"
def travisBuildNumber = System.getenv('TRAVIS_BUILD_NUMBER')
version = travisBuildNumber != null ? baseVersion + "." + travisBuildNumber : baseVersion

println 'Version: ' + version

group = 'com.jnericks'

repositories {
    mavenCentral()
}

apply plugin: 'groovy'

jar {
    from "LICENSE.md"
}

dependencies {
    compile "com.google.guava:guava:22.0"
    compile "junit:junit:4.12"
    compile "org.assertj:assertj-core:3.8.0"
    compile "org.mockito:mockito-core:2.8.47"
    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.8.0"
    testCompile "org.mockito:mockito-core:2.8.47"
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        publishTestLibJava(MavenPublication) {
            version version
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'TestLib-Java'
                    description 'Base Test Library for Java 8 Projects'
                    url "https://github.com/jnericks/testlib-java"
                    scm {
                        url "https://github.com/jnericks/testlib-java"
                        connection "https://github.com/jnericks/testlib-java"
                        developerConnection "https://github.com/jnericks/testlib-java"
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/jnericks/testlib-java/blob/master/LICENSE.md'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jnericks'
                            name 'Jon Erickson'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER ?: project["bintray.user"]
    key = System.env.BINTRAY_API_KEY ?: project["bintray.key"]
    publications = ['publishTestLibJava']
    publish = true
    pkg {
        repo = 'testlib-java'
        name = 'testlib-java'
        desc = 'Base Test Library for Java 8 Projects'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/jnericks/testlib-java'
        labels = ['test']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
            }
        }
    }
}

jacocoTestReport {
    group = "reporting"
    description = "Generate Jacoco coverage reports after running tests."
    additionalSourceDirs = files(sourceSets.main.allSource)
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

test.finalizedBy jacocoTestReport